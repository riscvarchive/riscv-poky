# Distribution definition for: poky-tiny
#
# Copyright (c) 2011, Intel Corporation.
# All rights reserved.
#
# This file is released under the MIT license as described in
# ../meta/COPYING.MIT.
#
# Poky-tiny is intended to define a tiny Linux system comprised of a
# Linux kernel tailored to support each specific MACHINE and busybox.
# Poky-tiny sets some basic policy to ensure a usable system while still
# keeping the rootfs and kernel image as small as possible.
#
# The policies defined are intended to meet the following goals:
# o Serial consoles only (no framebuffer or VGA console)
# o Basic support for IPV4 networking
# o Single user ash shell
# o Static images (no support for adding packages or libraries later)
# o Read-only or RAMFS root filesystem
# o Combined Linux kernel + rootfs in under 4MB
# o Allow the user to select between eglibc or uclibc with the TCLIBC variable
#
# This is currently a partial definition, the following tasks remain:
# [ ] Integrate linux-yocto-tiny ktype into linux-yocto
# [ ] Define linux-yocto-tiny configs for all supported BSPs
# [ ] Drop ldconfig from the installation
# [ ] Modify the runqemu scripts to work with ext2 parameter:
#     runqemu qemux86 qemuparams="-nographic" bootparams="console=ttyS0,115200 root=0800" 
# [ ] Modify busybox to allow for DISTRO_FEATURES-like confiruration

require conf/distro/poky.conf
DISTRO = "poky-riscv-tiny"

# FIXME: consider adding a new "tiny" feature
#DISTRO_FEATURES_append = " tiny"

# Distro config is evaluated after the machine config, so we have to explicitly
# set the kernel provider to override a machine config.
PREFERRED_PROVIDER_virtual/kernel = "linux-riscv"
PREFERRED_VERSION_linux-riscv = "4.1%"

# We can use packagegroup-core-boot, but in the future we may need a new packagegroup-core-tiny
#POKY_DEFAULT_EXTRA_RDEPENDS += "packagegroup-core-boot"
# Drop kernel-module-af-packet from RRECOMMENDS
POKY_DEFAULT_EXTRA_RRECOMMENDS = ""

# FIXME: what should we do with this?
TCLIBCAPPEND = ""

# Disable wide char support for ncurses as we don't include it in
# in the LIBC features below.
ENABLE_WIDEC="false"

# Drop native language support. This removes the
# eglibc->bash->gettext->libc-posix-clang-wchar dependency.
USE_NLS="no"

# Reconfigure eglibc for a smaller installation
# Comment out any of the lines below to disable them in the build
DISTRO_FEATURES_LIBC_TINY = "libc-libm libc-crypt"
#DISTRO_FEATURES_LIBC_TINY_append_x86-64 = " libc-libm-big"

# Required for "who"
DISTRO_FEATURES_LIBC_MINIMAL = "libc-utmp libc-getlogin"
DISTRO_FEATURES_LIBC_REGEX = "libc-posix-regexp"
DISTRO_FEATURES_LIBC_NET = "libc-inet libc-nis"

DISTRO_FEATURES_LIBC = "${DISTRO_FEATURES_LIBC_TINY} \
                        ${DISTRO_FEATURES_LIBC_MINIMAL} \
                        ${DISTRO_FEATURES_LIBC_REGEX} \
                        ${DISTRO_FEATURES_LIBC_NET} \
                       "

# Comment out any of the lines below to disable them in the build
# DISTRO_FEATURES options:
# alsa bluetooth ext2 irda pcmcia usbgadget usbhost wifi nfs zeroconf pci
#DISTRO_FEATURES_TINY = "pci"
#DISTRO_FEATURES_NET = "ipv4"
#DISTRO_FEATURES_USB = "usbhost"
#DISTRO_FEATURES_USBGADGET = "usbgadget"
#DISTRO_FEATURES_WIFI = "wifi"

DISTRO_FEATURES = "${DISTRO_FEATURES_TINY} \
                   ${DISTRO_FEATURES_NET} \
                   ${DISTRO_FEATURES_USB} \
                   ${DISTRO_FEATURES_USBGADGET} \
                   ${DISTRO_FEATURES_WIFI} \
                   ${DISTRO_FEATURES_LIBC} \
                  "

# Enable LFS - see bug YOCTO #5865
#DISTRO_FEATURES_append_libc-uclibc = " largefile"

# Use tmpdevfs and the busybox runtime services
VIRTUAL-RUNTIME_dev_manager = ""
VIRTUAL-RUNTIME_login_manager = ""
VIRTUAL-RUNTIME_init_manager = "riscv-init"
VIRTUAL-RUNTIME_keymaps = ""

# FIXME: Consider adding "modules" to MACHINE_FEATURES and using that in
# packagegroup-core-base to select modutils-initscripts or not.  Similar with "net" and
# netbase.

# By default we only support ext2 and initramfs. We don't build live as that
# pulls in a lot of dependencies for the live image and the installer, like
# udev, grub, etc.  These pull in gettext, which fails to build with wide
# character support.
IMAGE_FSTYPES = "ext2 cpio.gz"

# Drop v86d from qemu dependency list (we support serial)
# Drop grub from meta-intel BSPs
# FIXME: A different mechanism is needed here. We could define -tiny
#        variants of all compatible machines, but that leads to a lot
#        more machine configs to maintain long term.
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

# The mtrace script included by eglibc is a perl script. This means the system
# will build perl in case this package is installed. Since we don't care about
# this script for the purposes of tiny, remove the dependency from here.
RDEPENDS_${PN}-mtrace_pn-eglibc = ""

INHERIT += "blacklist"
PNBLACKLIST[build-appliance-image] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-base] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-clutter] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-directfb] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-full-cmdline] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-lsb] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-lsb-dev] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-lsb-sdk] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-rt] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-rt-sdk] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-sato] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-sato-dev] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-sato-sdk] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-x11] = "not buildable with poky-riscv"
PNBLACKLIST[qt4e-demo-image] = "not buildable with poky-riscv"
PNBLACKLIST[core-image-weston] = "not buildable with poky-riscv"

# Exclude packages not ported for riscv yet
PNBLACKLIST[eglibc] = "eglibc hasn't been ported for riscv yet"
PNBLACKLIST[libart-lgpl] = "need to generate art_config.h for riscv first"
PNBLACKLIST[ghostscript] = "need to generate objarch.h for riscv first"

# Disable python usage in opkg-utils since it won't build with tiny config
PACKAGECONFIG_pn-opkg-utils = ""

CORE_IMAGE_EXTRA_INSTALL += "glibc"

# Old GCC toolchain (riscv-tools)
#GCCVERSION = "4.6.1"
#SDKGCCVERSION = "${GCCVERSION}"
#BINUVERSION = "2.21%"
#GLIBCVERSION = "2.14%"

# New GCC toolchain
GCCVERSION = "4.9.1"
SDKGCCVERSION = "${GCCVERSION}"
BINUVERSION = "2.24"
GLIBCVERSION = "2.20"

# Set a couple of specific versions for the native utilities
PREFERRED_VERSION_binutils-native := "2.24"
PREFERRED_VERSION_gcc-native := "4.9%"

# Make sure Linux header versions and Linux version fit.
PREFERRED_VERSION_linux-libc-headers := "${PREFERRED_VERSION_linux-riscv}"
PREFERRED_VERSION_nativesdk-linux-libc-headers := "${PREFERRED_VERSION_linux-riscv}"
